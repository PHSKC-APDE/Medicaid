
-- Create slim table for temporary work
IF OBJECT_ID('tempdb..#temp') IS NOT NULL
DROP TABLE #temp;
SELECT
 CAST([CLNDR_YEAR_MNTH] AS INT) AS [CLNDR_YEAR_MNTH]
,CAST([MEDICAID_RECIPIENT_ID] AS VARCHAR(200)) AS [MEDICAID_RECIPIENT_ID]
,CAST([RAC_CODE] AS INT) AS [RAC_CODE]
,CAST([FROM_DATE] AS DATE) AS [FROM_DATE]
,CAST([TO_DATE] AS DATE) AS [TO_DATE]
,CAST([DUAL_ELIG] AS VARCHAR(200)) AS [DUAL_ELIG]
INTO #temp
FROM [dbo].[mcaid_elig_raw];

CREATE NONCLUSTERED INDEX [idx_nc_#temp] 
ON #temp([MEDICAID_RECIPIENT_ID], [CLNDR_YEAR_MNTH]);
GO

IF OBJECT_ID('[stage].[perf_elig_member_month]', 'U') IS NOT NULL
DROP TABLE [stage].[perf_elig_member_month];

WITH CTE AS
(
SELECT
 [CLNDR_YEAR_MNTH]
,[MEDICAID_RECIPIENT_ID]
--,[RPRTBL_RAC_CODE]
,[RAC_CODE]
,[FROM_DATE]
,[TO_DATE]
--,[MC_PRVDR_ID]
,[DUAL_ELIG]
--,[TPL_FULL_FLAG]
,ROW_NUMBER() OVER(PARTITION BY [MEDICAID_RECIPIENT_ID], [CLNDR_YEAR_MNTH] 
                   ORDER BY DATEDIFF(DAY, [FROM_DATE], [TO_DATE]) DESC) AS [row_num]
FROM #temp
)

SELECT
 [CLNDR_YEAR_MNTH]
,[MEDICAID_RECIPIENT_ID]
--,[RPRTBL_RAC_CODE]
,[RAC_CODE]
,[FROM_DATE]
,[TO_DATE]
--,[MC_PRVDR_ID]
,[DUAL_ELIG]
--,[TPL_FULL_FLAG]
INTO [stage].[perf_elig_member_month]
FROM CTE
WHERE [row_num] = 1;

ALTER TABLE [stage].[perf_elig_member_month] ALTER COLUMN [CLNDR_YEAR_MNTH] INT NOT NULL;
GO
ALTER TABLE [stage].[perf_elig_member_month] ALTER COLUMN [MEDICAID_RECIPIENT_ID] VARCHAR(200) NOT NULL;
GO
ALTER TABLE [stage].[perf_elig_member_month] ADD CONSTRAINT PK_stage_elig_member_month PRIMARY KEY ([MEDICAID_RECIPIENT_ID], [CLNDR_YEAR_MNTH]);
GO

/*
SELECT
 [CLNDR_YEAR_MNTH]
,COUNT(*)
FROM [dbo].[mcaid_elig_member_month]
WHERE [DUP_FLAG] = 0
GROUP BY [CLNDR_YEAR_MNTH]
ORDER BY [CLNDR_YEAR_MNTH];

SELECT
 [CLNDR_YEAR_MNTH]
,COUNT(*)
FROM [stage].[perf_elig_member_month]
GROUP BY [CLNDR_YEAR_MNTH]
ORDER BY [CLNDR_YEAR_MNTH];

SELECT NumRows
      ,COUNT(*)
FROM
(
SELECT [MEDICAID_RECIPIENT_ID]
      ,[CLNDR_YEAR_MNTH]
      ,COUNT(*) AS NumRows
FROM [stage].[perf_elig_member_month]
GROUP BY [MEDICAID_RECIPIENT_ID], [CLNDR_YEAR_MNTH]
) AS SubQuery
GROUP BY NumRows
ORDER BY NumRows;
*/